/-
Copyright (c) 2022 Thomas Browning. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Browning
-/

import group_theory.abelianization
import group_theory.transfer

/-!
# Schreier's Lemma

In this file we prove Schreier's lemma.

## Main results

- `closure_mul_image_eq` : **Schreier's Lemma**: If `R : set G` is a right_transversal
  of `H : subgroup G` with `1 ∈ R`, and if `G` is generated by `S : set G`,
  then `H` is generated by the `set` `(R * S).image (λ g, g * (to_fun hR g)⁻¹)`.
- `fg_of_index_ne_zero` : **Schreier's Lemma**: A finite index subgroup of a finitely generated
  group is finitely generated.
-/

open_locale pointwise

namespace subgroup

open mem_right_transversals

variables {G : Type*} [group G] {H : subgroup G} {R S : set G}

lemma closure_mul_image_mul_eq_top
  (hR : R ∈ right_transversals (H : set G)) (hR1 : (1 : G) ∈ R) (hS : closure S = ⊤) :
  (closure ((R * S).image (λ g, g * (to_fun hR g)⁻¹)) : set G) * R = ⊤ :=
begin
  let f : G → R := λ g, to_fun hR g,
  let U : set G := (R * S).image (λ g, g * (f g)⁻¹),
  change (closure U : set G) * R = ⊤,
  refine top_le_iff.mp (λ g hg, _),
  apply closure_induction_right (eq_top_iff.mp hS (mem_top g)),
  { exact ⟨1, 1, (closure U).one_mem, hR1, one_mul 1⟩ },
  { rintros - s hs ⟨u, r, hu, hr, rfl⟩,
    rw show u * r * s = u * ((r * s) * (f (r * s))⁻¹) * f (r * s), by group,
    refine set.mul_mem_mul ((closure U).mul_mem hu _) (f (r * s)).coe_prop,
    exact subset_closure ⟨r * s, set.mul_mem_mul hr hs, rfl⟩ },
  { rintros - s hs ⟨u, r, hu, hr, rfl⟩,
    rw show u * r * s⁻¹ = u * (f (r * s⁻¹) * s * r⁻¹)⁻¹ * f (r * s⁻¹), by group,
    refine set.mul_mem_mul ((closure U).mul_mem hu ((closure U).inv_mem _)) (f (r * s⁻¹)).2,
    refine subset_closure ⟨f (r * s⁻¹) * s, set.mul_mem_mul (f (r * s⁻¹)).2 hs, _⟩,
    rw [mul_right_inj, inv_inj, ←subtype.coe_mk r hr, ←subtype.ext_iff, subtype.coe_mk],
    apply (mem_right_transversals_iff_exists_unique_mul_inv_mem.mp hR (f (r * s⁻¹) * s)).unique
      (mul_inv_to_fun_mem hR (f (r * s⁻¹) * s)),
    rw [mul_assoc, ←inv_inv s, ←mul_inv_rev, inv_inv],
    exact to_fun_mul_inv_mem hR (r * s⁻¹) },
end

/-- **Schreier's Lemma**: If `R : set G` is a right_transversal of `H : subgroup G`
  with `1 ∈ R`, and if `G` is generated by `S : set G`, then `H` is generated by the `set`
  `(R * S).image (λ g, g * (to_fun hR g)⁻¹)`. -/
lemma closure_mul_image_eq
  (hR : R ∈ right_transversals (H : set G)) (hR1 : (1 : G) ∈ R) (hS : closure S = ⊤) :
  closure ((R * S).image (λ g, g * (to_fun hR g)⁻¹)) = H :=
begin
  have hU : closure ((R * S).image (λ g, g * (to_fun hR g)⁻¹)) ≤ H,
  { rw closure_le,
    rintros - ⟨g, -, rfl⟩,
    exact mul_inv_to_fun_mem hR g },
  refine le_antisymm hU (λ h hh, _),
  obtain ⟨g, r, hg, hr, rfl⟩ :=
  show h ∈ _, from eq_top_iff.mp (closure_mul_image_mul_eq_top hR hR1 hS) (mem_top h),
  suffices : (⟨r, hr⟩ : R) = (⟨1, hR1⟩ : R),
  { rwa [show r = 1, from subtype.ext_iff.mp this, mul_one] },
  apply (mem_right_transversals_iff_exists_unique_mul_inv_mem.mp hR r).unique,
  { rw [subtype.coe_mk, mul_inv_self],
    exact H.one_mem },
  { rw [subtype.coe_mk, inv_one, mul_one],
    exact (H.mul_mem_cancel_left (hU hg)).mp hh },
end

/-- **Schreier's Lemma**: If `R : set G` is a right_transversal of `H : subgroup G`
  with `1 ∈ R`, and if `G` is generated by `S : set G`, then `H` is generated by the `set`
  `(R * S).image (λ g, g * (to_fun hR g)⁻¹)`. -/
lemma closure_mul_image_eq_top
  (hR : R ∈ right_transversals (H : set G)) (hR1 : (1 : G) ∈ R) (hS : closure S = ⊤) :
  closure ((R * S).image (λ g, ⟨g * (to_fun hR g)⁻¹, mul_inv_to_fun_mem hR g⟩) : set H) = ⊤ :=
begin
  rw [eq_top_iff, ←map_subtype_le_map_subtype, monoid_hom.map_closure, set.image_image],
  exact (map_subtype_le ⊤).trans (ge_of_eq (closure_mul_image_eq hR hR1 hS)),
end

/-- **Schreier's Lemma**: If `R : finset G` is a right_transversal of `H : subgroup G`
  with `1 ∈ R`, and if `G` is generated by `S : finset G`, then `H` is generated by the `finset`
  `(R * S).image (λ g, g * (to_fun hR g)⁻¹)`. -/
lemma closure_mul_image_eq_top' [decidable_eq G] {R S : finset G}
  (hR : (R : set G) ∈ right_transversals (H : set G)) (hR1 : (1 : G) ∈ R)
  (hS : closure (S : set G) = ⊤) :
  closure ((((R * S).image (λ g, ⟨_, mul_inv_to_fun_mem hR g⟩)) : finset H) : set H) = ⊤ :=
begin
  rw [finset.coe_image, finset.coe_mul],
  exact closure_mul_image_eq_top hR hR1 hS,
end

lemma exists_finset_card_le_mul (hH : H.index ≠ 0) {S : finset G} (hS : closure (S : set G) = ⊤) :
  ∃ T : finset H, T.card ≤ H.index * S.card ∧ closure (T : set H) = ⊤ :=
begin
  haveI : decidable_eq G := classical.dec_eq G,
  obtain ⟨R₀, hR : R₀ ∈ right_transversals (H : set G), hR1⟩ := exists_right_transversal (1 : G),
  haveI : fintype (G ⧸ H) := fintype_of_index_ne_zero hH,
  haveI : fintype R₀ := fintype.of_equiv _ (mem_right_transversals.to_equiv hR),
  let R : finset G := set.to_finset R₀,
  replace hR : (R : set G) ∈ right_transversals (H : set G) := by rwa set.coe_to_finset,
  replace hR1 : (1 : G) ∈ R := by rwa set.mem_to_finset,
  refine ⟨_, _, closure_mul_image_eq_top' hR hR1 hS⟩,
  calc _ ≤ (R * S).card : finset.card_image_le
  ... ≤ (R ×ˢ S).card : finset.card_image_le
  ... = R.card * S.card : R.card_product S
  ... = H.index * S.card : congr_arg (* S.card) _,
  calc R.card = fintype.card R : (fintype.card_coe R).symm
  ... = _ : (fintype.card_congr (mem_right_transversals.to_equiv hR)).symm
  ... = fintype.card (G ⧸ H) : quotient_group.card_quotient_right_rel H
  ... = H.index : H.index_eq_card.symm,
end

/-- **Schreier's Lemma**: A finite index subgroup of a finitely generated
  group is finitely generated. -/
lemma fg_of_index_ne_zero [hG : group.fg G] (hH : H.index ≠ 0) : group.fg H :=
begin
  obtain ⟨S, hS⟩ := hG.1,
  obtain ⟨T, -, hT⟩ := exists_finset_card_le_mul hH hS,
  exact ⟨⟨T, hT⟩⟩,
end

lemma rank_le_index_mul_rank [hG : group.fg G] {H : subgroup G} (hH : H.index ≠ 0)
  [decidable_pred (λ n, ∃ (S : finset G), S.card = n ∧ closure (S : set G) = ⊤)]
  [decidable_pred (λ n, ∃ (S : finset H), S.card = n ∧ closure (S : set H) = ⊤)] :
  @group.rank H _ (fg_of_index_ne_zero hH) _ ≤ H.index * group.rank G :=
begin
  haveI := fg_of_index_ne_zero hH,
  obtain ⟨S, hS₀, hS⟩ := group.rank_spec G,
  obtain ⟨T, hT₀, hT⟩ := exists_finset_card_le_mul hH hS,
  calc group.rank H ≤ T.card : group.rank_le H hT
  ... ≤ H.index * S.card : hT₀
  ... = H.index * group.rank G : congr_arg ((*) H.index) hS₀,
end

open_locale big_operators

-- merging
lemma order_eq_card_zpowers' {G : Type*} [group G] (g : G) : order_of g = nat.card (zpowers g) :=
begin
  have := nat.card_congr (mul_action.orbit_zpowers_equiv g (1 : G)),
  rwa [nat.card_zmod, orbit_subgroup_one_eq_self, eq_comm] at this,
end

universes u v

-- PRed
@[simp] lemma nat.card_pi {α : Type u} {β : α → Type v} [fintype α] :
  nat.card (Π a, β a) = ∏ a, nat.card (β a) :=
begin
  classical,
  by_cases h1 : ∃ a, is_empty (β a),
  { haveI := is_empty_pi.mpr h1,
    obtain ⟨a, h⟩ := h1,
    rw [nat.card_of_is_empty, finset.prod_eq_zero (finset.mem_univ a)],
    exactI nat.card_of_is_empty },
  simp only [not_exists, not_is_empty_iff] at h1,
  by_cases h2 : ∀ a, finite (β a),
  { haveI := h2,
    haveI := λ a, fintype.of_finite (β a),
    simp only [nat.card_eq_fintype_card, fintype.card_pi] },
  simp only [not_forall, not_finite_iff_infinite] at h2,
  obtain ⟨a₀, h2⟩ := h2,
  rw [finset.prod_eq_zero (finset.mem_univ a₀)],
  { suffices : infinite (Π a, β a),
    { exactI nat.card_eq_zero_of_infinite },
    let f : (Π a, β a) → β a₀ := λ p, p a₀,
    have hf : function.surjective f :=
    λ b,  ⟨λ a, if h : a = a₀ then by rwa h else (h1 a).some, dif_pos rfl⟩,
    exactI infinite.of_surjective f hf },
  { exactI nat.card_eq_zero_of_infinite },
end

lemma key_lemma0 (G : Type*) [comm_group G] [group.fg G]
  [decidable_pred (λ n, ∃ (S : finset G), S.card = n ∧ closure (S : set G) = ⊤)] :
  nat.card G ∣ monoid.exponent G ^ group.rank G :=
begin
  classical,
  obtain ⟨S, hS1, hS2⟩ := group.rank_spec G,
  rw [←hS1, ←fintype.card_coe, ←finset.card_univ, ←finset.prod_const],
  let f : (Π g : S, zpowers (g : G)) →* G :=
  { to_fun := λ a, ∏ g : S, a g,
    map_one' := finset.prod_const_one,
    map_mul' := λ a b, finset.prod_mul_distrib },
  have hf : function.surjective f,
  { rw [←monoid_hom.range_top_iff_surjective, eq_top_iff, ←hS2, closure_le],
    intros g₀ hg₀,
    refine ⟨λ g, if ↑g = g₀ then ⟨g, mem_zpowers g⟩ else 1, _⟩,
    simp only [f, monoid_hom.coe_mk],
    refine (finset.prod_eq_single_of_mem (⟨g₀, hg₀⟩ : S) (finset.mem_univ ⟨g₀, hg₀⟩)
      (λ g _ hg, _)).trans (congr_arg coe (if_pos rfl)),
    have := mt subtype.ext hg,
    exact congr_arg coe (if_neg this) },
  replace hf := nat_card_dvd_of_surjective f hf,
  rw nat.card_pi at hf,
  refine hf.trans (finset.prod_dvd_prod_of_dvd _ _ (λ g hg, _)),
  rw ← order_eq_card_zpowers',
  exact monoid.order_dvd_exponent (g : G),
end

lemma key_lemma17 {G : Type*} [comm_group G] [group.fg G] {n : ℕ} (hG : ∀ g : G, g ^ n = 1)
  [decidable_pred (λ n, ∃ (S : finset G), S.card = n ∧ closure (S : set G) = ⊤)] :
  nat.card G ∣ n ^ group.rank G :=
(key_lemma0 G).trans
    (pow_dvd_pow_of_dvd (monoid.exponent_dvd_of_forall_pow_eq_one G n hG) (group.rank G))

@[to_additive]
lemma closure_preimage_eq_top {G : Type*} [group G] (s : set G) :
  closure ((closure s).subtype ⁻¹' s) = ⊤ :=
begin
  apply map_injective (show function.injective (closure s).subtype, from subtype.coe_injective),
  rwa [monoid_hom.map_closure, ←monoid_hom.range_eq_map, subtype_range,
    set.image_preimage_eq_of_subset],
  rw [coe_subtype, subtype.range_coe_subtype],
  exact subset_closure,
end

@[to_additive]
instance closure_finset_fg {G : Type*} [group G] (s : finset G) : group.fg (closure (s : set G)) :=
begin
  refine ⟨⟨s.preimage coe (subtype.coe_injective.inj_on _), _⟩⟩,
  rw finset.coe_preimage,
  exact closure_preimage_eq_top s,
end

@[to_additive]
instance closure_finite_fg {G : Type*} [group G] (s : set G) [finite s] : group.fg (closure s) :=
begin
  haveI := fintype.of_finite s,
  exact s.coe_to_finset ▸ subgroup.closure_finset_fg s.to_finset,
end

@[to_additive] lemma rank_closure_finset_le_card {G : Type*} [group G] (s : finset G)
  [decidable_pred (λ n, ∃ (S : finset (closure (s : set G))),
    S.card = n ∧ subgroup.closure (S : set (closure (s : set G))) = ⊤)] :
  group.rank (closure (s : set G)) ≤ s.card :=
begin
  classical,
  let t : finset (closure (s : set G)) := s.preimage coe (subtype.coe_injective.inj_on _),
  have ht : closure (t : set (closure (s : set G))) = ⊤,
  { rw finset.coe_preimage,
    exact closure_preimage_eq_top s },
  apply (group.rank_le (closure (s : set G)) ht).trans,
  rw [←finset.card_image_of_inj_on, finset.image_preimage],
  { apply finset.card_filter_le },
  { apply subtype.coe_injective.inj_on },
end

lemma rank_closure_finite_le_nat_card {G : Type*} [group G] (s : set G) [finite s]
  [decidable_pred (λ n, ∃ (S : finset (closure s)), S.card = n ∧ closure (S : set (closure s)) = ⊤)] :
  group.rank (closure s) ≤ nat.card s :=
begin
  classical,
  haveI := fintype.of_finite s,
  rw [nat.card_eq_fintype_card, ←set.to_finset_card],
  let t : finset (closure s) := s.to_finset.preimage coe (subtype.coe_injective.inj_on _),
  have ht : closure (t : set (closure s)) = ⊤,
  { rw [finset.coe_preimage, s.coe_to_finset],
    exact closure_preimage_eq_top s },
  apply (group.rank_le (closure s) ht).trans,
  rw [←finset.card_image_of_inj_on, finset.image_preimage],
  { apply finset.card_filter_le },
  { apply subtype.coe_injective.inj_on },
end

instance (G : Type*) [group G] [finite {g | ∃ g₁ g₂ : G, ⁅g₁, g₂⁆ = g}] :
  group.fg (commutator G) :=
begin
  haveI : finite {g | ∃ g₁ g₂ ∈ (⊤ : subgroup G), ⁅g₁, g₂⁆ = g},
  { simpa only [mem_top, exists_true_left] },
  apply subgroup.closure_finite_fg,
end

lemma rank_commutator_le_card (G : Type*) [group G] [finite {g | ∃ g₁ g₂ : G, ⁅g₁, g₂⁆ = g}]
  [decidable_pred (λ n, ∃ (S : finset (commutator G)), S.card = n ∧ closure (S : set (commutator G)) = ⊤)] :
  group.rank (commutator G) ≤ nat.card {g | ∃ g₁ g₂ : G, ⁅g₁, g₂⁆ = g} :=
begin
  haveI : finite {g | ∃ g₁ g₂ ∈ (⊤ : subgroup G), ⁅g₁, g₂⁆ = g},
  { simpa only [mem_top, exists_true_left] },
  apply (subgroup.rank_closure_finite_le_nat_card _).trans,
  simp only [mem_top, exists_true_left],
end

lemma card_commutator_dvd_index_center_pow [finite {g | ∃ g₁ g₂ : G, ⁅g₁, g₂⁆ = g}] :
  nat.card (commutator G) ∣ (center G).index ^ ((center G).index * nat.card {g | ∃ g₁ g₂ : G, ⁅g₁, g₂⁆ = g} + 1) :=
begin
  classical,
  by_cases hG : (center G).index = 0,
  { simp only [hG, zero_mul, zero_add, pow_one, dvd_zero] },
  let H := (center G).subgroup_of (commutator G),
  rw [←H.card_mul_index, pow_succ'],
  have h0 := relindex_dvd_index_of_normal (center G) (commutator G),
  have h2 := ne_zero_of_dvd_ne_zero hG h0,
  refine mul_dvd_mul (dvd_trans _ (pow_dvd_pow (center G).index ((rank_le_index_mul_rank h2).trans
    (nat.mul_le_mul (nat.le_of_dvd (nat.pos_of_ne_zero hG) h0) (rank_commutator_le_card G))))) h0,
  apply key_lemma17 (λ g, _),
  have key := subtype.ext_iff.mp (abelianization.commutator_subset_ker
    (monoid_hom.transfer_center_pow' hG) g.1.2),
  exact subtype.ext (subtype.ext key),
end

-- bounded commutators and bounded index of center implies bounded commutator subgroup (DONE!)

-- bounded commutators implies bounded index of center
-- * reduce to the case where G is finitely generated by passing to the subgroup generated by the elements occuring in list of commutators
-- * center equals intersection of finitely many centralizers
-- * each centralizer has finite index (i.e., each conjugacy class is finite)
-- * h * g * h⁻¹ = h * g * h⁻¹ * g⁻¹ * g = [h, g] * g

-- And then we can start Neumann...

-- bounded index of center implies bounded commutators (less important)

end subgroup
