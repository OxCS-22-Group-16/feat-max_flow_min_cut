/-
Copyright (c) 2022 R√©mi Bottinelli. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©mi Bottinelli
-/
import category_theory.groupoid.vertex_group
import category_theory.groupoid
import category_theory.groupoid.basic
--import category_theory.groupoid.free_groupoid
import algebra.group.defs
import algebra.hom.group
import algebra.hom.equiv
import data.set.lattice
import combinatorics.quiver.connected_component
import combinatorics.quiver.subquiver
import group_theory.subgroup.basic
/-!
# Subgroupoid

This file defines subgroupoids as `structure`s containing the subsets of arrows and their
stability under composition and inversion.
Also defined are

* containment of subgroupoids is a complete lattice;
* images and preimages of subgroupoids under a functor;
* the notion of normality of subgroupoids and its stability under intersection and preimage;
* compatibility of the above with `groupoid.vertex_group`.


## Main definitions

Given a type `C` with associated `groupoid C` instance.

* `subgroupoid C` is the type of subgroupoids of `C`
* `subgroupoid.is_normal` is the property that the subgroupoid is stable under conjugation
  by arbitrary arrows, _and_ that all identity arrows are contained in the subgroupoid.
* `subgroupoid.comap` is the "preimage" map of subgroupoids along a functor.
* `subgroupoid.map` is the "image" map of subgroupoids along a functor _injective on objects_.
* `subgroupoid.vertex_subgroup` is the subgroup of the `vertex group` at a given vertex `v`,
  assuming `v` is contained in the `subgroupoid` (meaning, by definition, that the arrow `ùüô v`
  is contained in the subgroupoid).

## Implementation details

The structure of this file is copied from/inspired by `group_theory.subgroup.basic`
and `combinatorics.simple_graph.subgraph`.

## TODO

* Equivalent inductive characterization of generated (normal) subgroupoids.
* Characterization of normal subgroupoids as kernels.

## Tags

subgroupoid

-/

open set classical function
local attribute [instance] prop_decidable

namespace category_theory

namespace groupoid

universes u v

variables {C : Type u} [groupoid C]


/--
A sugroupoid of `C` consists of a choice of arrows for each pair of vertices, closed
under composition and inverses
-/
@[ext] structure subgroupoid (C : Type u) [groupoid C] :=
  (arrws : ‚àÄ (c d : C), set (c ‚ü∂ d))
  (inv' : ‚àÄ {c d} {p : c ‚ü∂ d} (hp : p ‚àà arrws c d),
            inv p ‚àà arrws d c)
  (mul' : ‚àÄ {c d e} {p} (hp : p ‚àà arrws c d) {q} (hq : q ‚àà arrws d e),
            p ‚â´ q ‚àà arrws c e)


namespace subgroupoid

-- Why can't I use this?
instance mem_subgroupoid (c d : C) : has_mem (c ‚ü∂ d) (subgroupoid C) :=
‚ü® Œª f S, f ‚àà S.arrws c d ‚ü©

variable (S : subgroupoid C)

/-- The vertices of `C` on which `S` has non-trivial isotropy -/
def carrier : set C := {c : C | (S.arrws c c).nonempty }

lemma id_mem_of_nonempty_isotropy (c : C) :
  c ‚àà carrier S ‚Üí ùüô c ‚àà S.arrws c c :=
begin
  rintro ‚ü®Œ≥,hŒ≥‚ü©,
  have : ùüô c = Œ≥ * (inv Œ≥), by simp only [vertex_group_mul, comp_inv],
  rw this,
  apply S.mul' hŒ≥ (S.inv' hŒ≥),
end

/-- A subgroupoid seen as a quiver on vertex set `C` -/
def as_wide_quiver : quiver C := ‚ü®Œª c d, subtype $ S.arrws c d‚ü©

/-- Type synonym to coerce a subgroupoid into a groupoid -/
def coe := subtype $ S.carrier

/-- A subgroupoid as a groupoid -/
@[simps] def coe_groupoid : groupoid (coe S) :=
{ to_category :=
  { to_category_struct :=
    { to_quiver :=
      { hom := Œª a b, S.arrws a.val b.val },
      id := Œª a, ‚ü®ùüô a.val, id_mem_of_nonempty_isotropy S a.val a.prop‚ü©,
      comp := Œª a b c p q, ‚ü®p.val ‚â´ q.val, S.mul' p.prop q.prop‚ü©, },
    id_comp' := Œª a b ‚ü®p,hp‚ü©, by simp only [category.id_comp],
    comp_id' := Œª a b ‚ü®p,hp‚ü©, by simp only [category.comp_id],
    assoc' := Œª a b c d ‚ü®p,hp‚ü© ‚ü®q,hq‚ü© ‚ü®r,hr‚ü©, by simp only [category.assoc] },
  inv := Œª a b p, ‚ü®inv p.val, S.inv' p.prop‚ü©,
  inv_comp' := Œª a b ‚ü®p,hp‚ü©, by simp only [inv_comp],
  comp_inv' := Œª a b ‚ü®p,hp‚ü©, by simp only [comp_inv] }

/-- The `groupoid` instance for `subgroupoid.coe` -/
instance (S : subgroupoid C) : groupoid S.coe := coe_groupoid S

/-- There is an embedding of the coerced subgroupoid to its parent-/
def coe_embedding : (coe S) ‚•§ C :=
{ obj := Œª c, c.val,
  map := Œª c d f, f.val,
  map_id' := Œª c, by simp only [subtype.val_eq_coe, coe_groupoid_to_category_id_coe],
  map_comp' := Œª c d e f g, by simp only [subtype.val_eq_coe, coe_groupoid_to_category_comp_coe] }

lemma coe_embedding.inj_on_objects : function.injective (coe_embedding S).obj := by
{ rintros ‚ü®c,hc‚ü© ‚ü®d,hd‚ü© hcd, simp only [subtype.mk_eq_mk], exact hcd }

lemma coe_embedding.faithful :
  ‚àÄ c d, function.injective (Œª (f : c ‚ü∂ d), (coe_embedding S).map f) := by
{ rintros ‚ü®c,hc‚ü© ‚ü®d,hd‚ü© ‚ü®f,hf‚ü© ‚ü®g,hg‚ü© hfg, simp only [subtype.mk_eq_mk], exact hfg, }

/-- The subgroup of the vertex group at `c` given by the subgroupoid -/
def vertex_subgroup {c : C} (hc : c ‚àà S.carrier) : subgroup (c ‚ü∂ c) :=
‚ü® S.arrws c c,
  Œª f g hf hg, S.mul' hf hg,
  by {apply id_mem_of_nonempty_isotropy, use hc,},
  Œª f hf, S.inv' hf‚ü©

/-- `S` is a subgroupoid of `T` if it is contained in it -/
def is_subgroupoid (S T : subgroupoid C) : Prop :=
  ‚àÄ {c d}, S.arrws c d ‚äÜ T.arrws c d

instance subgroupoid_le : has_le (subgroupoid C) := ‚ü®is_subgroupoid‚ü©

lemma le_refl (S : subgroupoid C) : S ‚â§ S :=
by {rintro c d p, exact id,}

lemma le_trans (R S T : subgroupoid C) : R ‚â§ S ‚Üí S ‚â§ T ‚Üí R ‚â§ T :=
by {rintro RS ST c d, exact (@RS c d).trans (@ST c d), }

lemma le_antisymm (R S : subgroupoid C) : R ‚â§ S ‚Üí S ‚â§ R ‚Üí R = S :=
by {rintro RS SR, ext c d p, exact ‚ü®(@RS c d p), (@SR c d p)‚ü©,}

instance : partial_order (subgroupoid C) :=
{ le := is_subgroupoid,
  le_refl := le_refl,
  le_trans := le_trans,
  le_antisymm := le_antisymm}

instance : has_top (subgroupoid C) :=
‚ü®‚ü®(Œª _ _, set.univ), by { rintros, trivial, }, by { rintros, trivial, }‚ü©‚ü©
instance : has_bot (subgroupoid C) :=
‚ü®‚ü®(Œª _ _, ‚àÖ), by { rintros, exfalso, assumption, }, by { rintros, exfalso, assumption, }‚ü©‚ü©

instance : inhabited (subgroupoid C) := ‚ü®‚ä§‚ü©

instance : has_inf (subgroupoid C) :=
‚ü® Œª S T,
  ‚ü®(Œª c d, (S.arrws c d)‚à©(T.arrws c d)),
    by { rintros, exact ‚ü®S.inv' hp.1,T.inv' hp.2‚ü©, },
    by { rintros, exact ‚ü®S.mul' hp.1 hq.1, T.mul' hp.2 hq.2‚ü©, }‚ü©‚ü©

instance : has_Inf (subgroupoid C) :=
‚ü® Œª s,
  ‚ü®(Œª c d, set.Inter (Œª (S : s), S.val.arrws c d)),
    by
    { rintros,
      simp only [Inter_coe_set, mem_Inter] at hp ‚ä¢,
      rintro S Ss,
      exact S.inv' (hp S Ss)},
    by
    { rintros,
      simp only [Inter_coe_set, mem_Inter] at hp hq ‚ä¢,
      rintro S Ss,
      apply S.mul' (hp S Ss) (hq S Ss), }‚ü©‚ü©

instance : complete_lattice (subgroupoid C) :=
{ bot          := (‚ä•),
  bot_le       := Œª S c d, by {apply empty_subset,},
  top          := (‚ä§),
  le_top       := Œª S c d, by {apply subset_univ,},
  inf          := (‚äì),
  le_inf       := Œª R S T RS RT c d p pR, ‚ü®RS pR, RT pR‚ü©,
  inf_le_left  := Œª R S c d p pRS, pRS.left,
  inf_le_right := Œª R S c d p pRS, pRS.right,
  .. complete_lattice_of_Inf (subgroupoid C)
       ( by
        { dsimp only [Inf], rintro s, constructor,
          { rintro S Ss c d p hp,
            simp only [Inter_coe_set, mem_Inter] at hp,
            exact hp S Ss, },
          { rintro T Tl c d p pT,
            simp only [Inter_coe_set, mem_Inter],
            rintros S Ss, apply Tl Ss, exact pT,}}) }

/-- The family of arrows of the full subgroupoid on vertex set `V` -/
inductive full_on.arrws (V : set C) : Œ† (c d : C), (c ‚ü∂ d) ‚Üí Prop
| intro {c : C} (hc : c ‚àà V) {d : C} (hd : d ‚àà V) (f : c ‚ü∂ d) : full_on.arrws c d f

@[simp] lemma full_on.mem_arrws_iff (V : set C) {c d : C} (f : c ‚ü∂ d) :
  full_on.arrws V c d f ‚Üî c ‚àà V ‚àß d ‚àà V :=
begin
  split,
  { rintros ‚ü®c,hc,d,hd,f‚ü©, exact ‚ü®hc,hd‚ü©, },
  { rintros ‚ü®hc,hd‚ü©, constructor; assumption, },
end

/-- The full subgroupoid on vertex set `V` -/
def full_on (V : set C) : subgroupoid C :=
‚ü® full_on.arrws V,
  by { rintros, induction hp, constructor; assumption, },
  by { rintros, induction hp, induction hq, constructor; assumption } ‚ü©

/-- The family of arrows of the discrete subgroupoid -/
inductive discrete.arrws : Œ† (c d : C), (c ‚ü∂ d) ‚Üí Prop
| id (c : C) : discrete.arrws c c (ùüô c)

/-- The only arrows of the discrete subgroupoid are the identity arrows-/
def discrete : subgroupoid C :=
‚ü® discrete.arrws,
  by { rintros _ _ _ hp, induction hp, simp only [inv_eq_inv, is_iso.inv_id], constructor, },
  by { rintros _ _ _ _ hp _ hq, induction hp, induction hq, rw category.comp_id, constructor,} ‚ü©

lemma mem_discrete_iff {c d : C} (f : c ‚ü∂ d):
  (f ‚àà (discrete).arrws c d) ‚Üî (‚àÉ (h : c = d), f = h.rec_on (ùüô c)) :=
begin
  split,
  { intro hf, induction hf, simp only [eq_self_iff_true, exists_true_left], },
  { rintro ‚ü®h,he‚ü©, subst_vars, constructor, }
end

section normal

/-- A¬†subgroupoid is normal if it is ‚Äúwide‚Äù (meaning that its carrier set is all of `C`)
    and satisfies the expected stability under conjugacy -/
structure is_normal : Prop :=
  (wide : ‚àÄ c, (ùüô c) ‚àà (S.arrws c c))
  (conj : ‚àÄ {c d} (p : c ‚ü∂ d) (Œ≥ : c ‚ü∂ c) (hs : Œ≥ ‚àà S.arrws c c),
                ((inv p) ‚â´ Œ≥ ‚â´ p) ‚àà (S.arrws d d))
  (conj' : ‚àÄ {c d} (p : d ‚ü∂ c) (Œ≥ : c ‚ü∂ c) (hs : Œ≥ ‚àà S.arrws c c),
                (p ‚â´ Œ≥ ‚â´ (inv p)) ‚àà (S.arrws d d)
         := Œª c d p Œ≥ hs, by { convert conj (inv p) Œ≥ hs, simp, })

lemma is_normal.conjugation_eq (Sn : is_normal S) {c d} (p : c ‚ü∂ d) :
  set.bij_on (Œª Œ≥ : c ‚ü∂ c, (inv p) ‚â´ Œ≥ ‚â´ p) (S.arrws c c) (S.arrws d d) :=
begin
  split,
  { rintro Œ≥ Œ≥S, apply Sn.conj, exact Œ≥S },
  split,
  { rintro Œ≥‚ÇÅ Œ≥‚ÇÅS Œ≥‚ÇÇ Œ≥‚ÇÇS h,
    let := p ‚â´=(h =‚â´ (inv p)),
    simp only [inv_eq_inv, category.assoc, is_iso.hom_inv_id, category.comp_id,
               is_iso.hom_inv_id_assoc] at this ‚ä¢,
    exact this, }, -- what's the quickest way here?
  { rintro Œ¥ Œ¥S, use (p ‚â´ Œ¥ ‚â´ (inv p)), split,
    { have : p = inv (inv p), by {simp only [inv_eq_inv, is_iso.inv_inv]},
      nth_rewrite 0 this,
      apply Sn.conj, exact Œ¥S, },
    { simp only [category.assoc, inv_comp, category.comp_id],
      simp only [‚Üêcategory.assoc, inv_comp, category.id_comp], }}
end

lemma top_is_normal : is_normal (‚ä§ : subgroupoid C) :=
{ wide := (Œª c, trivial),
  conj := (Œª a b c d e, trivial) }

lemma discrete_is_normal : is_normal (discrete : subgroupoid C) :=
{ wide := (Œª c, discrete.arrws.id c),
  conj := (Œª a b f d e , by
  { simp only [mem_discrete_iff, eq_self_iff_true, inv_eq_inv, is_iso.inv_comp_eq,
               category.comp_id, exists_true_left] at e ‚ä¢,
    simp only [e, category.id_comp], }) }

lemma Inf_is_normal (s : set $ subgroupoid C) (sn : ‚àÄ S ‚àà s, is_normal S) : is_normal (Inf s) :=
{ wide := by
  { rintro c _ ‚ü®‚ü®S,Ss‚ü©,rfl‚ü©,
    exact (sn S Ss).wide c, },
  conj := by
  { rintros c d p Œ≥ hŒ≥ _ ‚ü®‚ü®S,Ss‚ü©,rfl‚ü©,
    apply (sn S Ss).conj p Œ≥,
    apply hŒ≥,
    use ‚ü®S,Ss‚ü©, } }

lemma is_normal.vertex_subgroup (Sn : is_normal S) (c : C) (cS : c ‚àà S.carrier) :
  (S.vertex_subgroup cS).normal :=
begin
  constructor,
  rintros x hx y,
  simp only [vertex_group_mul, vertex_group.inv_eq_inv, category.assoc],
  have : y = inv (inv y), by { simp only [inv_eq_inv, is_iso.inv_inv], },
  nth_rewrite 0 this,
  simp only [‚Üêinv_eq_inv],
  apply Sn.conj, exact hx,
end

end normal


section graph_like

abbreviation is_graph_like := is_graph_like S.coe

lemma is_graph_like_iff : S.is_graph_like ‚Üî ‚àÄ c d, subsingleton (S.arrws c d) := sorry

end graph_like

section disconnected

abbreviation is_disconnected := is_disconnected S.coe

lemma is_disconnected_iff : S.is_disconnected ‚Üî ‚àÄ c d, c ‚â† d ‚Üí is_empty (S.arrws c d) := sorry

/-- The arrow set of `disconnect`, which drops all arrows but the loops -/
inductive disconnect.arrws : Œ†  (c d : C), (c ‚ü∂ d) ‚Üí Prop
| vert {c : C} {Œ≥ : c ‚ü∂ c} :  disconnect.arrws c c Œ≥

lemma disconnect.mem_arrws_iff {c d : C} (f : c ‚ü∂ d) :
  disconnect.arrws c d f ‚Üî  c = d :=
begin
  split,
  { rintros ‚ü®c,Œ≥‚ü©, simp only [eq_self_iff_true, exists_true_left], },
  { rintro rfl, constructor, },
end

/-- Only keep the loops of `S` -/
def disconnect (C) [groupoid C] : subgroupoid C :=
‚ü® disconnect.arrws,
  Œª _ _ f hf, by {induction hf, constructor, },
  Œª _ _ _ f hf g hg, by
  { induction hf, induction hg, constructor, }‚ü©

lemma disconnect_is_disconnected : groupoid.is_disconnected  (disconnect C).coe :=
begin
  rintro c d ne, by_contradiction,
  simp only [coe_groupoid_to_category_hom, is_empty_coe_sort] at h,
  sorry,
end

lemma disconnect_is_normal_of_normal (Sn : is_normal S) : (disconnect S.coe).is_normal := sorry

end disconnected

section hom

variables {D : Type*}
variables [groupoid D] (œÜ : C ‚•§ D)

section comap

variables (T U : subgroupoid D)

/--
A functor between groupoid defines a map of subgroupoids in the reverse direction
by taking preimages.
 -/
def comap : subgroupoid C :=
‚ü® Œª c d, {f : c ‚ü∂ d | œÜ.map f ‚àà T.arrws (œÜ.obj c) (œÜ.obj d)},
  by
  { rintros,
    simp only [inv_eq_inv, mem_set_of_eq, functor.map_inv],
    simp only [‚Üêinv_eq_inv],
    simp only [mem_set_of_eq] at hp,
    apply T.inv', assumption, },
  by
  { rintros,
    simp only [mem_set_of_eq, functor.map_comp],
    apply T.mul';
    assumption, }‚ü©


lemma comap_mono {T U} :
  T ‚â§ U ‚Üí comap œÜ T ‚â§ comap œÜ U :=
begin
  rintro TU,
  dsimp only [subgroupoid.comap],
  rintro c d p hp,
  exact TU hp,
end

lemma is_normal_comap {T} (Tn : is_normal T) : is_normal (comap œÜ T) :=
{ wide := by
  { rintro c,
    dsimp only [comap],
    simp only [mem_set_of_eq, functor.map_id],
    apply Tn.wide, },
  conj := by
  { rintros c d f Œ≥ hŒ≥,
    dsimp only [comap],
    simp only [mem_set_of_eq, functor.map_comp, functor.map_inv, inv_eq_inv],
    rw [‚Üêinv_eq_inv],
    apply Tn.conj, exact hŒ≥, } }

/-- The kernel of a functor between subgroupoid is the preimage. -/
def ker : subgroupoid C := comap œÜ (discrete)

lemma mem_ker_iff {c d : C} (f : c ‚ü∂ d) :
  f ‚àà (ker œÜ).arrws c d ‚Üî ‚àÉ (h : œÜ.obj c = œÜ.obj d), œÜ.map f = h.rec_on (ùüô $ œÜ.obj c) :=
mem_discrete_iff (œÜ.map f)

end comap

section map

variables (hœÜ : function.injective œÜ.obj) (S)

/-- The family of arrows of the image of a subgroupoid under a functor injective on objects -/
inductive map.arrws :
  Œ† (c d : D), (c ‚ü∂ d) ‚Üí Prop
| im {c d : C} (f : c ‚ü∂ d) (hf : f ‚àà S.arrws c d) : map.arrws (œÜ.obj c) (œÜ.obj d) (œÜ.map f)

lemma map.mem_arrws_iff {c d : D} (f : c ‚ü∂ d) :
  map.arrws S œÜ c d f ‚Üî
  ‚àÉ (a b : C) (g : a ‚ü∂ b) (ha : œÜ.obj a = c) (hb : œÜ.obj b = d) (hg : g ‚àà S.arrws a b),
    f = @eq.rec_on _ (œÜ.obj a) (Œª x, x ‚ü∂ d) (c) ha (hb.rec_on $ œÜ.map g) :=
begin
  split,
  { rintro ‚ü®a,b,g,hg‚ü©, use [a,b,g,rfl,rfl,hg,rfl], },
  { rintro ‚ü®a,b,g,ha,hb,hg,he‚ü©, subst_vars,
    simp only [congr_arg_mpr_hom_right, eq_to_hom_refl, category.comp_id],
    constructor, exact hg, },
end

/-- The "forward" image of a subgroupoid under a functor injective on objects -/
def map (hœÜ : function.injective œÜ.obj) (S) : subgroupoid D :=
‚ü® map.arrws S œÜ,
  by
  { rintro _ _ _ hp, induction hp,
    rw [inv_eq_inv,‚Üêfunctor.map_inv], constructor,
    rw ‚Üêinv_eq_inv, apply S.inv', assumption, },
  by -- Is there no way to prove this ‚Üì directly without the help of `map.mem_arrws_iff` ?
  { rintro _ _ _ _ hp _ hq,
    obtain ‚ü®f‚ÇÄ,f‚ÇÅ,f,hf‚ÇÄ,hf‚ÇÅ,hf,fp‚ü© := (map.mem_arrws_iff S œÜ p).mp hp,
    obtain ‚ü®g‚ÇÄ,g‚ÇÅ,g,hg‚ÇÄ,hg‚ÇÅ,hg,gq‚ü© := (map.mem_arrws_iff S œÜ q).mp hq,
    simp only [has_mem.mem, map.mem_arrws_iff],
    have : f‚ÇÅ = g‚ÇÄ, by {apply hœÜ, exact hf‚ÇÅ.trans hg‚ÇÄ.symm, },
    induction this,
    refine ‚ü®f‚ÇÄ,g‚ÇÅ,f ‚â´ g,hf‚ÇÄ,hg‚ÇÅ,S.mul' hf hg,_‚ü©,
    simp only [functor.map_comp],
    subst_vars } ‚ü©

lemma map_mono (S T : subgroupoid C) :
  S ‚â§ T ‚Üí map œÜ hœÜ S ‚â§ map œÜ hœÜ T :=
begin
  rintros le _ _ _ ‚ü®a,b,f,h‚ü©,
  constructor,
  apply le h,
end

/-- The image of a functor injective on objects -/
def im := map œÜ hœÜ (‚ä§)

lemma mem_im_iff {c d : D} (f : c ‚ü∂ d) :
  f ‚àà (im œÜ hœÜ).arrws c d ‚Üî
  ‚àÉ (a b : C) (g : a ‚ü∂ b) (ha : œÜ.obj a = c) (hb : œÜ.obj b = d),
    f = @eq.rec_on _ (œÜ.obj a) (Œª x, x ‚ü∂ d) (c) ha (hb.rec_on $ œÜ.map g) :=
begin
  convert map.mem_arrws_iff ‚ä§ œÜ f,
  dsimp [‚ä§,has_top.top],
  simp only [mem_univ, exists_true_left],
end

lemma is_normal_map (hœÜs : im œÜ hœÜ = ‚ä§) :
  S.is_normal ‚Üí (map œÜ hœÜ S).is_normal := sorry

lemma is_graph_like_im : is_graph_like C ‚Üí  is_graph_like (im œÜ hœÜ).coe := sorry

lemma is_disconnected_im : is_disconnected C ‚Üí is_disconnected (im œÜ hœÜ).coe := sorry

end map

end hom

section generated_subgroupoid

-- TODO:¬†proof that generated is just "words in X" and generated_normal is similarly
variable (X : ‚àÄ (c d : C), set (c ‚ü∂ d))

/-- The subgropoid generated by the set of arrows `X` -/
def generated : subgroupoid C :=
  Inf {S : subgroupoid C | ‚àÄ c d, X c d ‚äÜ S.arrws c d}

lemma generated_contains : ‚àÄ c d, X c d ‚äÜ (generated X).arrws c d := sorry

lemma generated_le_of_containing : (‚àÄ c d, X c d ‚äÜ S.arrws c d) ‚Üí (generated X) ‚â§ S :=
  @Inf_le (subgroupoid C) _ {S : subgroupoid C | ‚àÄ c d, X c d ‚äÜ S.arrws c d} S


def as_quiver := {c : C // ‚àÉ d, (X c d).nonempty ‚à® (X d c).nonempty }
instance as_quiver_quiver : quiver (as_quiver X) := ‚ü®Œª c d, subtype $ X c.val d.val‚ü©
def incl : prefunctor (as_quiver X) C :=
{ obj := Œª c, c.val,
  map := Œª c d f, f.val }

lemma incl_injective : function.injective (incl X).obj := by
{ rintros x y he, ext, exact he, }
lemma incl_faithful {x y : as_quiver X} : function.injective (Œª (f : x ‚ü∂ y), (incl X).map f) := by
{ rintros f g he, ext, exact he, }

/-
lemma generated_is_lift_free_groupoid :
  (generated X) = im (free.lift (incl X)) (free.lift_of_injective (incl X) $ incl_injective X) :=
begin
  apply le_antisymm,
  { apply generated_le_of_containing,
    rintro c d f hf, dsimp [im, map],
    let cc := (@free.of (as_quiver X) _).obj (‚ü®c, ‚ü®d, by {left, constructor, exact hf}‚ü©‚ü© : as_quiver X),
    let dd := (@free.of (as_quiver X) _).obj (‚ü®d, ‚ü®c, by {right, constructor, exact hf}‚ü©‚ü© : as_quiver X),
    let ff : cc ‚ü∂ dd :=  (@free.of (as_quiver X) _).map ‚ü®f,hf‚ü©,

    have ccc : c = (free.lift $ incl X).obj cc, by sorry,
    have ddd : d = (free.lift $ incl X).obj dd, by sorry,
    have fff : f = (free.lift $ incl X).map ff, by sorry,
    rw fff,
    dsimp [free.lift,incl,quotient.lift,paths.lift,quiver.symmetrify.lift,‚ä§] at *,
    sorry




     }
end
-/
/-- The normal sugroupoid generated by the set of arrows `X` -/
def generated_normal : subgroupoid C :=
  Inf {S : subgroupoid C | (‚àÄ c d, X c d ‚äÜ S.arrws c d) ‚àß S.is_normal }

lemma generated_normal_is_normal : (generated_normal X).is_normal :=
Inf_is_normal _ (Œª S h, h.right)

end generated_subgroupoid


end subgroupoid

end groupoid

end category_theory
